%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<&| /_elements/wrapper, title => $title &>
<& /Elements/Tabs,
    current_subtab => $current_subtab,
    title => $title,
    dashboard_obj => $Dashboard &>

<& /Elements/ListActions, actions => \@results &>

% if (@hidden_searches) {
<& Elements/HiddenSearches, searches => \@hidden_searches, dashboard => $Dashboard &>
% }

<table width="100%" border="0">
% for my $pane (@panes) {
<tr><td valign="top" class="boxcontainer">
<form action="Queries.html" name="Dashboard-<%$pane->{name}%>" method="post" enctype="multipart/form-data">
<input type="hidden" class="hidden" name="id" value="<%$Dashboard->id%>" />
<input type="hidden" class="hidden" name="privacy" value="<%$Dashboard->privacy%>" />
<&| /Widgets/TitleBox, title => $pane->{display_name} &>
    <& /Widgets/SelectionBox:show, self => $pane, nojs => 1 &>
</&>
</form>
</td></tr>
% }

</table>
</&>
<%INIT>

my $current_subtab = 'Dashboards/Queries.html?id=' . $id;
my @results;

use RT::Dashboard;
my $Dashboard = new RT::Dashboard( current_user => Jifty->web->current_user );
my ($ok, $msg) = $Dashboard->load_by_id($id);
$ok || abort(_("Couldn't load dashboard %1: %2", $id, $msg));
my $title = _("Modify the queries of dashboard %1", $Dashboard->name);

my %desc_of;
my @items;
my %selected;

# Get the list of queries already in use
do {
    my $panes = $Dashboard->panes;
    for my $pane (keys %$panes) {
        for my $portlet (@{ $panes->{$pane} }) {
            my $name;
            if ($portlet->{portlet_type} eq 'search') {
                $name = join '-', 'search', $portlet->{id}, $portlet->{privacy};
            }
            else {
                $name = join '-', 'component', $portlet->{component};
            }

            push @{ $selected{$pane} }, $name;
            $desc_of{$name} = $portlet->{description};
        }
    }
};

my @components = @{ RT->config->get('homepage_components') };
my %allowed_components = map {$_ => 1} @components;

for my $desc (@components) {
    my $name = "component-$desc";
    push @items, [$name, $desc];
    $desc_of{$name} = $desc;
}

my $sys = RT::System->new( current_user => Jifty->web->current_user );
my @objs = ($sys);

push @objs, RT::SavedSearches->new( current_user => Jifty->web->current_user )->_privacy_objects
    if Jifty->web->current_user->has_right( right  => 'LoadSavedSearch',
                                          object => RT->system_user );

for my $object (@objs) {
    for ($m->comp("/Search/Elements/SearchesForObject", object => $object)) {
        my ($desc, $search) = @$_;
        my $SearchType = $search->content->{'SearchType'} || 'Ticket';
        my $privacy = $Dashboard->_build_privacy($object);
        my $name = 'search-' . $search->id . '-' . $privacy;
        push @items, [$name, $desc];
        $desc_of{$name} = $desc;
    }
}

# Create selectionbox widgets for those queries
my %pane_name = (
    'body'    => _('Body'),
    'sidebar' => _('Sidebar'),
);

my @panes;
for my $pane (keys %pane_name) {
    my $sel = $m->comp(
        '/Widgets/SelectionBox:new',
        action      => 'Queries.html',
        name        => "Searches-$pane",
        display_name => $pane_name{$pane},
        available   => \@items,
        selected    => $selected{$pane},
        auto_save    => 1,
        on_submit    => sub {
            my $self = shift;

            my @portlets;
            for (@{ $self->{Current} }) {
                my $item = $_;
                my $desc = $desc_of{$item};
                my $portlet_type = $1 if $item =~ s/^(\w+)-//;

                if ($portlet_type eq 'search') {
                    my ($search_id, $privacy) = split '-', $item, 2;
                    push @portlets, {
                        portlet_type => $portlet_type,
                        privacy      => $privacy,
                        id           => $search_id,
                        description  => $desc,
                        pane         => $pane,
                    };
                }
                elsif ($portlet_type eq 'component') {
                    push @portlets, {
                        portlet_type => $portlet_type,
                        component    => $item,
                        path         => "/Elements/$item",
                        description  => $item,
                        pane         => $pane,
                    };
                }
            }

            # we want to keep all the other panes the same
            my $panes = $Dashboard->panes;
            $panes->{$pane} = \@portlets;

            my ($ok, $msg) = $Dashboard->update(panes => $panes);

            if ($ok) {
                push @results, _("Dashboard updated");
            }
            else {
                push @results, _("Dashboard could not be updated: %1", $msg);
            }
        },
    );

    push @panes, $sel;
}

$m->comp('/Widgets/SelectionBox:process', %ARGS, self => $_, nojs => 1)
    for @panes;

my @hidden_searches = $Dashboard->possible_hidden_searches;

</%INIT>
<%ARGS>
$id => '' unless defined $id
</%ARGS>

