<&| /Admin/Elements/Header, title => $title &>

<& /Elements/Tabs,
    title       => $title,
    current_tab => 'Admin/Global/Workflows/Mappings.html',
&>

<& /Elements/ListActions, actions => \@results &>

<&|/l&>Mapping between schemas is required when you want to move tickets between queues with different schemas.</&>

% unless ( @list > 1 ) {
<&|/l&>You need more than one workflow to be able to define mappings.</&>
% } else {
<& Elements/MissingMaps &>

<form action="<% RT->config->get('web_path') %>/Admin/Global/Workflows/Mappings.html" method="get">
<& /Widgets/Form/Select,
    name          => 'from',
    description   => _('From'),
    values        => \@list,
    current_value => $from,
    default       => 0,
&>
<& /Widgets/Form/Select,
    name          => 'to',
    description   => _('To'),
    values        => \@list,
    current_value => $to,
    default       => 0,
&>

<& /Elements/Submit, label => _('Select'), name => 'select' &>
</form>
% }

% if ( $from && $to ) {
<form action="<% RT->config->get('web_path') %>/Admin/Global/Workflows/Mappings.html" method="post">
<input type="hidden" name="from" value=<% $from %> />
<input type="hidden" name="to" value=<% $to %> />

<&|/Widgets/TitleBox, title => "$from -> $to" &>
% unless ( $from_schema->has_map( $to ) ) {
<&|/l&>Don't forget to save your changes as this mapping is only suggested defaults.</&>
% }
% foreach my $status ( $from_schema->valid ) {
% my $current = $from_schema->map( $to_schema )->{ lc $status };
% $current = $status if !$current && $to_schema->is_valid( $status );
% $current = ($to_schema->valid( $from_schema->from_set( $status ) ))[0] unless $current;
<& /Widgets/Form/Select,
    name          => "from @ $status ->",
    description   => _($status) .' -> ',
    values        => [ $to_schema->valid ],
    default       => 0,
    empty         => 0,
    current_value => $current,
&></br>
% }
</&>

<&|/Widgets/TitleBox, title => "$to -> $from" &>
% unless ( $to_schema->has_map( $from ) ) {
<&|/l&>Don't forget to save your changes as this mapping is only suggested defaults.</&>
% }
% foreach my $status ( $to_schema->valid ) {
% my $current = $to_schema->map( $from_schema )->{ lc $status };
% $current = $status if !$current && $from_schema->is_valid( $status );
% $current = ($from_schema->valid( $to_schema->from_set( $status ) ))[0] unless $current;
<& /Widgets/Form/Select,
    name          => "to @ $status ->",
    description   => _($status) .' -> ',
    values        => [ $from_schema->valid ],
    default       => 0,
    empty         => 0,
    current_value => $current,
&></br>
% }
</&>

<& /Elements/Submit, label => _('Update'), name => 'update' &>
</form>
% }
</&>

<%INIT>
my $title = _("Modify mapping between workflows");

use RT::Workflow;
my @list = RT::Workflow->new->list;

my @results;
my ($from_schema, $to_schema);
if ( $from ) {
    $from_schema = RT::Workflow->load( $from );
    $from_schema = $from = undef unless $from_schema;
}
if ( $to ) {
    $to_schema = RT::Workflow->load( $to );
    $to_schema = $to = undef unless $to_schema;
}

if ( $update && $from_schema && $to_schema ) {
    my %map = ();
    foreach my $s ( $from_schema->valid ) {
        $map{ lc $s } = $ARGS{"from @ $s ->"};
    }
    my ($status, $msg) = $from_schema->set_map( $to_schema, %map );
    push @results, $msg if $msg;

    %map = ();
    foreach my $s ( $to_schema->valid ) {
        $map{ lc $s } = $ARGS{"to @ $s ->"};
    }
    ($status, $msg) = $to_schema->set_map( $from_schema, %map );
    push @results, $msg if $msg;
}

</%INIT>
<%ARGS>
$from   => undef
$to     => undef
$update => undef
$select => undef
</%ARGS>
