%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<&| /Admin/Elements/Header, title => $title &>
<& /Elements/Tabs,
    id => $CustomFieldObj->id ,
    current_tab => $current_tab,
    title => $title &>
<& /Elements/ListActions, actions => \@results &>


<form method="post" action="Modify.html" name="modify_custom_field">
<input type="hidden" class="hidden" name="id" value="<% $id %>" />

<table>

<tr><td class="label"><&|/l&>name</&></td>
<td><input name="name" value="<% $CustomFieldObj->name || '' %>" size="20" /></td></tr>

<tr><td class="label"><&|/l&>description</&></td>
<td><input name="description" value="<% $CustomFieldObj->description || '' %>" size="80" /></td></tr>

<tr><td class="label"><&|/l&>Type</&></td>
<td><& /Admin/Elements/SelectCustomFieldType, 
        name => "type_composite", 
        default => $CustomFieldObj->type_composite, &>
</td></tr>

% if ( $CustomFieldObj->id and $CustomFieldObj->is_selection_type and RT->config->get('custom_field_values_sources') and ( scalar(@{RT->config->get('custom_field_values_sources')}) > 0 ) ) {
    
<tr><td class="label"><&|/l&>Field values source:</&></td><td>
<& /Admin/Elements/EditCustomFieldValuesSource, custom_field => $CustomFieldObj &>
</td></tr>
% }

<tr><td class="label"><&|/l&>Applies to</&></td>
<td><& /Admin/Elements/SelectCustomFieldLookupType, 
        name => "lookup_type", 
        default => $CustomFieldObj->lookup_type, &>
</td></tr>

<tr><td class="label"><&|/l&>Validation</&></td>
<td><& /Widgets/ComboBox,
    name    => 'pattern',
    default => $CustomFieldObj->pattern,
    size    => 20,
    values  => \@CFvalidations,
&></td></tr>

<tr><td class="label"><&|/l&>Link values to</&></td><td>
<input size="60" name="link_value_to"  value="<% $CustomFieldObj->link_value_to || '' %>" />
<div class="hints">
<&|/l&>RT can make this custom field's values into hyperlinks to another service.</&>
<&|/l&>Fill in this field with a URL.</&>
<&|/l&>RT will replace <tt>__id__</tt> and <tt>__CustomField__</tt> with the record id and custom field value, respectively</&>
</div></td></tr>

<tr><td class="label"><&|/l&>Include page</&></td><td>
<input size="60" name="include_content_for_value" value="<% $CustomFieldObj->include_content_for_value || '' %>" />
<div class="hints">
<&|/l&>RT can include content from another web service when showing this custom field.</&>
<&|/l&>Fill in this field with a URL.</&>
<&|/l&>RT will replace <tt>__id__</tt> and <tt>__CustomField__</tt> with the record id and custom field value, respectively</&>
<i><&|/l&>Some browsers may only load content from the same domain as your RT server.</&></i>
</div></td></tr>

<tr><td class="label">&nbsp;</td><td>
<input type="hidden" class="hidden" name="set_enabled" value="1" />
<input type="checkbox" class="checkbox" name="enabled" value="1" <% $enabled_checked |n%> />
<&|/l&>enabled (Unchecking this box disables this custom field)</&>
</td></tr>

</table>

% if ( $CustomFieldObj->id && $CustomFieldObj->is_selection_type && !$CustomFieldObj->is_external_values ) {
<h2><&|/l&>Values</&></h2>
<div>
<& /Admin/Elements/EditCustomFieldValues, custom_field => $CustomFieldObj &>
<& /Admin/Elements/AddCustomFieldValue, custom_field => $CustomFieldObj &>
</div>
% }

<& /Elements/Submit, name => 'update', label => $id eq 'new'? _('Create'): _('Save Changes') &>

</form>
</&>
<%INIT>
my ($title, @results, $disabled);

my $CustomFieldObj = RT::Model::CustomField->new( current_user => Jifty->web->current_user );
my $current_tab = 'Admin/CustomFields/Modify.html';

unless ( $id ) {
    $title = _("Create a CustomField");
    $id    = 'new';
    $current_tab .= '?create=1';
}
else {
    if ( $id eq 'new' ) {
        my ( $val, $msg ) = $CustomFieldObj->create(
            name          => $name,
            type_composite => $type_composite,
            lookup_type    => $lookup_type,
            description   => $description,
            pattern       => $pattern,
            link_value_to   => $link_value_to,
            include_content_for_value => $include_content_for_value,
        );
        $m->comp( "/Elements/Error", why => _( "Could not create CustomField", $msg ) ) unless $val;
        push @results, $msg;
        $title = _( 'Created CustomField %1', $CustomFieldObj->name );
    } else {
        $CustomFieldObj->load( $id ) || $m->comp("/Elements/Error", why => _('No CustomField') );
        $title = _( 'Editing CustomField %1', $CustomFieldObj->name );
    }
    $current_tab .= "?id=$id";
}

if ( $ARGS{'update'} && $id ne 'new' ) {

    #we're asking about enabled on the web page but really care about disabled.
    $ARGS{'disabled'} = $disabled = $enabled? 0 : 1;

    my @attribs = qw(disabled pattern name type_composite lookup_type description link_value_to include_content_for_value);
    push @results, update_record_object(
        attributes_ref => \@attribs,
        object        => $CustomFieldObj,
        args_ref       => \%ARGS
    );
    $CustomFieldObj->set_values_class( $values_class );

    my $paramtag = "CustomField-". $CustomFieldObj->id ."-value";
    # Delete any fields that want to be deleted
    foreach my $key ( keys %ARGS ) {
        next unless $key =~ /^Delete-$paramtag-(\d+)$/o;
        my ($val, $msg) = $CustomFieldObj->delete_value( $1 );
        push (@results, $msg);
    }

    # Update any existing values
    my $values = $CustomFieldObj->values_obj;
    while ( my $value = $values->next ) {
        foreach my $attr qw(name description sort_order ) {
            my $param = "$paramtag-". $value->id ."-". $attr;
            next unless exists $ARGS{$param};
            next if ($value->$attr()||'') eq ($ARGS{$param}||'');

            my $mutator = "set_$attr";
            my ($id, $msg) = $value->$mutator( $ARGS{$param} );
            push (@results, $msg);
        }
    }

    # Add any new values
    if ( defined $ARGS{ $paramtag ."-new-name" } && length $ARGS{ $paramtag ."-new-name" } ) {
        my ($id, $msg) = $CustomFieldObj->add_value(
            map { $_ => $ARGS{ $paramtag ."-new-$_" } }
                qw( name description sort_order )
        );
        push (@results, $msg);
    }
}

$id = $CustomFieldObj->id if $CustomFieldObj->id;

my $enabled_checked = qq[checked="checked"];
$enabled_checked = '' if $CustomFieldObj->disabled;

my @CFvalidations = (
    '(?#Mandatory).',
    '(?#Digits)^[\d.]+$',
    '(?#Year)^[12]\d{3}$',
);

$m->callback(callback_name => 'ValidationPatterns', values => \@CFvalidations);


</%INIT>
<%ARGS>
$id => undef
$type_composite => undef
$lookup_type => undef
$max_values => undef
$sort_order => undef
$description => undef
$pattern => undef
$name => undef
$set_enabled => undef
$enabled => 0
$values_class => 'RT::Model::CustomFieldValueCollection'
$link_value_to => undef
$include_content_for_value => undef
</%ARGS>
