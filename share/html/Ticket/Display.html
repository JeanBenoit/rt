%# BEGIN BPS TAGGED BLOCK {{{
%# 
%# COPYRIGHT:
%# 
%# This software is Copyright (c) 1996-2008 Best Practical Solutions, LLC
%#                                          <jesse@bestpractical.com>
%# 
%# (Except where explicitly superseded by other copyright notices)
%# 
%# 
%# LICENSE:
%# 
%# This work is made available to you under the terms of Version 2 of
%# the GNU General Public License. A copy of that license should have
%# been provided with this software, but in any event can be snarfed
%# from www.gnu.org.
%# 
%# This work is distributed in the hope that it will be useful, but
%# WITHOUT ANY WARRANTY; without even the implied warranty of
%# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU
%# General Public License for more details.
%# 
%# You should have received a copy of the GNU General Public License
%# along with this program; if not, write to the Free Software
%# Foundation, Inc., 51 Franklin Street, Fifth Floor, Boston, MA
%# 02110-1301 or visit their web page on the internet at
%# http://www.gnu.org/licenses/old-licenses/gpl-2.0.html.
%# 
%# 
%# CONTRIBUTION SUBMISSION POLICY:
%# 
%# (The following paragraph is not intended to limit the rights granted
%# to you to modify and distribute this software under the terms of
%# the GNU General Public License and is only of importance to you if
%# you choose to contribute your changes and enhancements to the
%# community by submitting them to Best Practical Solutions, LLC.)
%# 
%# By intentionally submitting any modifications, corrections or
%# derivatives to this work, or any other work intended for use with
%# Request Tracker, to Best Practical Solutions, LLC, you confirm that
%# you are the copyright holder for those contributions and you grant
%# Best Practical Solutions,  LLC a nonexclusive, worldwide, irrevocable,
%# royalty-free, perpetual, license to use, copy, create derivative
%# works based on those contributions, and sublicense and distribute
%# those contributions and any derivatives thereof.
%# 
%# END BPS TAGGED BLOCK }}}
<& /Elements/Header,
    title => _("#%1: %2", $ticket_obj->id, $ticket_obj->subject), 
    link_rel => \%link_rel &>
<& /Elements/Tabs, 
    ticket => $ticket_obj, 
    current_tab => 'Ticket/Display.html?id='.$ticket_obj->id,
    title => _("#%1: %2", $ticket_obj->id, $ticket_obj->subject) &>

% $m->callback(callback_name => 'BeforeActionList', %ARGS, actions => \@Actions, args_ref => \%ARGS, ticket => $ticket_obj);

<& /Elements/ListActions, actions => \@Actions &>
<& Elements/ShowUpdateStatus, ticket => $ticket_obj &>

% $m->callback( %ARGS, ticket => $ticket_obj, callback_name => 'BeforeShowSummary' );
<div class="summary">
<&| /Widgets/TitleBox, title => _('Ticket metadata') &>
<& /Ticket/Elements/ShowSummary,  ticket => $ticket_obj, attachments => $attachments &>
</&>
</div>
<br />

% $m->callback( ticket => $ticket_obj, %ARGS, callback_name => 'BeforeShowHistory' );

<& /Ticket/Elements/ShowHistory , 
      ticket => $ticket_obj, 
      tickets => $Tickets, 
      collapsed => $ARGS{'collapsed'}, 
      show_headers => $ARGS{'show_headers'},
      attachments => $attachments,
      attachment_content => $attachment_content
&> 

<%perl>

 $m->callback( %ARGS,
     ticket       => $ticket_obj,
     current_tab  => 'Ticket/Display.html?id=' . $ticket_obj->id,
     callback_name => 'AfterShowHistory',
 );
 $m->comp('/Elements/Footer');
 </%perl> 
 <%ARGS>
$id => undef
$ticket_obj => undef
$show_headers => 0
$collapsed => undef
</%ARGS>

<%INIT>

$m->callback( ticket_obj => $ticket_obj, args_ref => \%ARGS, callback_name => 'Initial' );

my (@Actions, $Tickets);  


unless ($id || $ticket_obj) {
    abort('No ticket specified');
}

if ($ARGS{'id'} eq 'new') {
    # {{{ Create a new ticket

    my $queue = RT::Model::Queue->new( current_user => Jifty->web->current_user );
    $queue->load($ARGS{'queue'});
    unless ( $queue->id ) {
        abort('Queue not found');
    }

    unless ( $queue->current_user_has_right('CreateTicket') ) {
        abort('You have no permission to create tickets in that queue.');
    }

    ($ticket_obj, @Actions) = create_ticket(
        Attachments => Jifty->web->session->get('Attachments'),
        %ARGS,
    );
    Jifty->web->session->remove('Attachments');

#XXX bloody hack, should have a better way
    my %cf_args;
    for my $arg ( keys %ARGS ) {
        if ( $arg =~ /J:A:F-(\d+)-edit-ticket-cfs/ ) {
            $cf_args{$1} = $ARGS{$arg};
        }
    }
    my $cf_action = RT::Action::EditTicketCFs->new( arguments => { id =>
            $ticket_obj->id, %cf_args } );
    $cf_action->take_action;

    unless ( $ticket_obj->current_user_has_right('ShowTicket') ) {
        abort("No permission to view newly Created ticket #".$ticket_obj->id.".");
    }
    # }}}
} else { 
    $ticket_obj ||= load_ticket($ARGS{'id'});

    $m->callback( callback_name => 'BeforeProcessArguments',
        ticket_obj => $ticket_obj, tickets => $Tickets,
        ActionsRef => \@Actions, args_ref => \%ARGS );
    if ( defined $ARGS{'action'} ) {
        if ($ARGS{'action'} =~ /^(steal|kill|take|set_told)$/) {
            my $action = $1;
            my ($res, $msg) = $ticket_obj->$action();
            push(@Actions, $msg);
        }
    }

    $m->callback(callback_name => 'ProcessArguments', 
            ticket => $ticket_obj, 
            args_ref => \%ARGS, 
            actions => \@Actions);
    
    $ARGS{update_attachments} = Jifty->web->session->get('Attachments');
    push @Actions,
        process_update_message(
        args_ref   => \%ARGS,
        actions   => \@Actions,
        ticket_obj => $ticket_obj,
        );
    Jifty->web->session->remove('Attachments');

    #Process status updates
    push @Actions, process_ticket_watchers(args_ref => \%ARGS, ticket_obj => $ticket_obj);
    push @Actions, process_ticket_basics(args_ref => \%ARGS, ticket_obj => $ticket_obj);
    push @Actions, process_ticket_links(args_ref => \%ARGS, ticket_obj => $ticket_obj);
    push @Actions, process_ticket_dates(args_ref => \%ARGS, ticket_obj => $ticket_obj);
    push @Actions, process_object_custom_field_updates(args_ref => \%ARGS, ticket_obj => $ticket_obj);

    # XXX: we shouldn't block actions here if user has no right to see the ticket,
    # but we should allow him to see actions he has done
    unless ($ticket_obj->current_user_has_right('ShowTicket')) {
        abort("No permission to view ticket");
    }
    if ( $ARGS{'mark_as_seen'} ) {
        $ticket_obj->set_attribute(
            name => 'User-'. $ticket_obj->current_user->id .'-SeenUpTo',
            content => $ticket_obj->last_updated,
        );
        push @Actions, _('Marked all messages as seen');
    }
}

$m->callback(
    callback_name => 'BeforeDisplay',
    ticket_obj => \$ticket_obj,
    tickets => \$Tickets,
    actions => \@Actions,
    args_ref => \%ARGS,
);

# This code does automatic redirection if any updates happen. 

if (@Actions) {

    # We've done something, so we need to clear the decks to avoid
    # resubmission on refresh.
    # But we need to store Actions somewhere too, so we don't lose them.
    my $key = Digest::MD5::md5_hex( rand(1024) );
    Jifty->web->session->set( "actions_$key" => \@Actions );
    RT::Interface::Web::redirect( Jifty->web->url . "Ticket/Display.html?id=" . $ticket_obj->id . "&results=" . $key );
}

my $attachments = $m->comp('Elements/FindAttachments', ticket => $ticket_obj, tickets => $Tickets);
my $attachment_content = $m->comp('Elements/LoadTextAttachments', ticket => $ticket_obj);

my %link_rel;
if (defined Jifty->web->session->get('tickets') and ($ARGS{'query'} or Jifty->web->session->get('CurrentSearchHash')->{'Query'})) {
    my $item_map = Jifty->web->session->get('tickets')->item_map;
    $link_rel{first} = "Ticket/Display.html?id=" . $item_map->{first}                if $item_map->{$ticket_obj->id}{prev};
    $link_rel{prev}  = "Ticket/Display.html?id=" . $item_map->{$ticket_obj->id}{prev} if $item_map->{$ticket_obj->id}{prev};
    $link_rel{next}  = "Ticket/Display.html?id=" . $item_map->{$ticket_obj->id}{next} if $item_map->{$ticket_obj->id}{next};
    $link_rel{last}  = "Ticket/Display.html?id=" . $item_map->{last}                 if $item_map->{$ticket_obj->id}{next};
}
</%INIT>
